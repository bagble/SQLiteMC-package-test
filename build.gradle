plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.0'
    id 'java'
    id 'maven-publish'
}

java.toolchain.languageVersion.set(JavaLanguageVersion.of(16))

allprojects {
    repositories {
        mavenCentral()
        maven {
            name = 'papermc-repo'
            url = 'https://papermc.io/repo/repository/maven-public/'
        }
        maven {
            name = 'jitpack'
            url = 'https://jitpack.io'
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'com.github.johnrengelman.shadow'

    dependencies {
        compileOnly ('com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT')

        testImplementation ('org.junit.jupiter:junit-jupiter-api:5.7.0')
        testRuntimeOnly ('org.junit.jupiter:junit-jupiter-engine:5.7.0')
    }

    publishing {
        repositories {
            maven {
                name = "github"
                url = "https://maven.pkg.github.com/bagble/${rootProject.name}" as Object

                credentials {
                    username = "bagble"
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }

        publications {
            "maven"(MavenPublication) {
                groupId = project.properties.get("pluginGroup").toString()
                version = project.properties.get("pluginVersion").toString()

                pom {
                    name.set(rootProject.name)
                    url.set("https://github.com/bagble/${rootProject.name}")

                    licenses {
                        license {
                            name.set("MIT License")
                            url.set("https://opensource.org/licenses/MIT")
                        }
                    }

                    scm {
                        connection.set("scm:git:git://github.com/bagble/${rootProject.name}.git")
                        developerConnection.set("scm:git:ssh://github.com:bagble/${rootProject.name}.git")
                        url.set("https://github.com/bagble/${rootProject.name}")
                    }
                }

                artifact(jar)
            }
        }
    }
}